plugins {

  id 'java'
  id 'com.github.johnrengelman.shadow' version '7.1.2'

}

group 'unprotesting.com.github'
version '0.13.1'

java {

  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }

}

repositories {

  mavenCentral()
  maven { url 'https://jitpack.io' }
  maven { url 'https://ci.ender.zone/plugin/repository/everything/' }
  maven { url 'https://papermc.io/repo/repository/maven-public/' }
  maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
  maven {
    name "essentialsx-releases" 
    url "https://repo.essentialsx.net/releases/"
  }

}

dependencies {

  compileOnly('com.github.milkbowl:VaultAPI:1.7.1') {
    exclude group: "org.bukkit", module: "bukkit"
  }
  
  implementation 'org.bstats:bstats-bukkit:2.2.1'
  implementation 'org.mapdb:mapdb:3.0.8'
  implementation 'com.github.stefvanschie.inventoryframework:IF:0.10.6'

  compileOnly 'io.papermc.paper:paper-api:1.19-R0.1-SNAPSHOT'
  compileOnly 'net.kyori:adventure-api:4.11.0'
  compileOnly 'net.kyori:adventure-text-minimessage:4.11.0'
  compileOnly 'org.projectlombok:lombok:1.18.24'
  compileOnly 'net.essentialsx:EssentialsX:2.19.4'

  annotationProcessor 'org.projectlombok:lombok:1.18.24'

  testImplementation 'org.mapdb:mapdb:3.0.8'
  testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-api:5.8.1'
}


shadowJar {

  dependencies {
    exclude(dependency('org.eclipse.collections:eclipse-collections-api'))
    exclude(dependency('org.eclipse.collections:eclipse-collections'))
    exclude(dependency('org.eclipse.collections:eclipse-collections-forkjoin'))
    exclude(dependency('com.google.guava:guava'))
    exclude(dependency('net.jpountz.lz4:lz4'))
    exclude(dependency('org.jetbrains.kotlin:kotlin-stdlib'))
    exclude(dependency('com.google.code.findbugs:jsr305'))
    exclude(dependency('com.google.errorprone:error_prone_annotations'))
    exclude(dependency('com.google.guava:failureaccess'))
    exclude(dependency('com.google.guava:listenablefuture'))
    exclude(dependency('com.google.j2objc:j2objc-annotations'))
    exclude(dependency('org.checkerframework:checker-qual'))
    exclude(dependency('org.jetbrains:annotations'))
    exclude(dependency('org.jetbrains.kotlin:kotlin-stdlib-common'))
  }

  relocate "org.bstats", "unprotesting.com.github.bstats"
  relocate "org.mapdb", "unprotesting.com.github.mapdb"
  relocate "com.github.stefvanschie.inventoryframework", "unprotesting.com.github.inventoryframework"

  minimize()

  archiveName("Auto-Tune-${project.version}.jar")

}

artifacts {
  archives shadowJar
}

test {
  useJUnitPlatform()
  maxHeapSize = '1G'
}